plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.41'
}

group 'com.vjgarcia'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
    maven { url "https://plugins.gradle.org/m2/" }
    jcenter()
}

configurations {
    detekt
    ktlint
}

task detekt(type: JavaExec, group: "verification") {
    main = "io.gitlab.arturbosch.detekt.cli.Main"
    classpath = configurations.detekt
    def input = "$projectDir"
    def config = "$projectDir/config/detekt.yml"
    def params = ['-i', input, '-c', config]
    args(params)
}
check.dependsOn detekt

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    def params = ["src/**/*.kt", "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/ktlint.xml"]
    args(params)
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    def params = ["-F", "src/**/*.kt"]
    args(params)
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation 'io.github.seik.kotlin-telegram-bot:telegram:0.3.2'

    // Koin
    implementation "org.koin:koin-core:2.0.1"
    testImplementation "org.koin:koin-test:2.0.1"

    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.0-RC'

    // JSoup
    implementation 'org.jsoup:jsoup:1.11.3'

    // Exposed
    implementation 'org.jetbrains.exposed:exposed:0.16.2'
    implementation "org.xerial:sqlite-jdbc:3.21.0.1"

    // job scheduler
    implementation 'net.greghaines:jesque:2.1.3'

    // Hamcrest
    testImplementation 'com.natpryce:hamkrest:1.4.2.0'

    // Mockito
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.0.0-RC1"

    detekt "io.gitlab.arturbosch.detekt:detekt-cli:1.0.0.RC9.2"
    ktlint "com.github.shyiko:ktlint:0.20.0"
}
